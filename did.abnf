; This document combines information from the DID, URI and ABNF specifications
; to describe a complete grammar for did and did-reference strings.
;
; DID Spec:  https://w3c-ccg.github.io/did-spec/#the-generic-did-scheme
; URI Spec:  https://tools.ietf.org/html/rfc3986
; ABNF Spec: https://tools.ietf.org/html/rfc5234

did                = "did:" method-name ":" method-specific-id
method-name        = 1*method-char
method-char        = %x61-7A / DIGIT
method-specific-id = *idchar *( ":" *idchar )
idchar             = ALPHA / DIGIT / "." / "-" / "_"
did-url            = did *( ";" param ) path-abempty [ "?" query ]
                     [ "#" fragment ]
param              = param-name [ "=" param-value ]
param-name         = 1*param-char
param-value        = *param-char
param-char         = ALPHA / DIGIT / "." / "-" / "_" / ":" /
                     pct-encoded



; A generic DID path is identical to a URI path and MUST conform to the path-abempty ABNF rule in [RFC3986]
; https://tools.ietf.org/html/rfc3986#section-3.3
path-abempty  = *( "/" segment )

; A generic DID fragment is identical to a URI fragment and MUST conform to the fragment ABNF rule in [RFC3986]
; https://tools.ietf.org/html/rfc3986#section-3.5
fragment      = *( pchar / "/" / "?" )

; A generic DID query is identical to a URI query and MUST conform to the query ABNF rule in [RFC3986]
; https://tools.ietf.org/html/rfc3986#section-3.4
query         = *( pchar / "/" / "?" )

segment           = *pchar
segment-nz        = 1*pchar
pchar             = unreserved / pct-encoded / sub-delims / ":" / "@"
unreserved        = ALPHA / DIGIT / "-" / "." / "_" / "~"
pct-encoded       = "%" HEXDIG HEXDIG
sub-delims        = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="

; https://tools.ietf.org/html/rfc5234
; ALPHA           =  %x41-5A / %x61-7A
; HEXDIG          =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"
; DIGIT           =  %x30-39

; http://www.columbia.edu/kermit/ascii.html
; 41-5A is big letters A-Z in US-ASCII
; 61-7A is small letters a-z in US-ASCII
; 30-39 is digits 0-9 in US-ASCII
